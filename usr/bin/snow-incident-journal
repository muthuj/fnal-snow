#!/usr/bin/env perl

=head1 NAME

snow-incident-journal - adds a journal entry for a Service Now incident

=head1 SYNOPSIS

B<snow-incident-journal> F<INCIDENT_NUMBER> F<TEXT>

B<snow-incident-journal> --man

=head1 DESCRIPTION

snow-incident-journal adds new journal entries for a specified Incident number.
The updated Incident information is then printed to STDOUT.

=cut

##############################################################################
### Configuration ############################################################
##############################################################################

# these should move
use lib '/home/tskirvin/rpm/fnal-snow/lib';
our $CONFIG_FILE = '/home/tskirvin/rpm/fnal-snow/etc/snow/config.yaml';

our $DEBUG = 0;
our $TYPE  = 'work_notes';
our $USER  = $ENV{'REMOTE_USER'} || $ENV{'USER'} || getpwuid ($>);

##############################################################################
### Declarations #############################################################
##############################################################################

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use FNAL::SNOW;

$|++;

##############################################################################
### Subroutines ##############################################################
##############################################################################

### error_usage (ERROR)
# Exit out with pod2usage.
sub error_usage {
    my ($error) = @_;
    pod2usage (-msg => $error, -exit_status => 2, -verbose => 1);
}

### debug (MSG)
# Print a debugging message if $DEBUG is set.
sub debug { if ($DEBUG) { warn "@_\n" } }

##############################################################################
### main () ##################################################################
##############################################################################

my $parser = Getopt::Long::Parser->new();
my $result = $parser->getoptions (
    'd|debug'  => \$DEBUG,
    'c|config' => \$CONFIG_FILE,
    'u|user=s' => \$USER,
    't|type=s' => \$TYPE,
    'man'      => sub { pod2usage (-verbose => 2, -noperldoc => 1) },
    'h|help'   => sub { pod2usage (-verbose => 1) }) || error_usage ();

error_usage ("invalid type: $TYPE") unless $TYPE =~ /^(work_notes|comment)$/;

my ($incnum, @rest) = @ARGV;
error_usage ("no incident listed") unless ($incnum);

my $text = join (' ', @rest);
error_usage ('no text provided') if $text eq '';

debug "Creating FNAL::SNOW object: $CONFIG_FILE";
my $SNOW = FNAL::SNOW->init ('config_file' => $CONFIG_FILE, 'debug' => $DEBUG);
my $CONFIG = $SNOW->config_hash;

debug "Connecting to ServiceNow at $CONFIG->{servicenow}->{url}";
$SNOW->connect or die "could not connect to SN\n";

debug "Looking up incident '$incnum'";
my @incident = $SNOW->incident_by_number ($incnum);
unless (@incident) { die "unable to load incident: '$incnum'\n"; }
unless (scalar @incident == 1) {
    die "too many matches for incident '$incnum'\n";
}
my $inc = $incident[0];

debug "Adding journal entry";

my @return = $SNOW->tkt_update ($$inc{'number'},
    $TYPE => "$USER: $text", 'type' => $TYPE);

my $return = 0;
if (scalar @return > 1) {
    print "too many matches after update\n";
    $return = 2;
} elsif (scalar @return < 1) {
    print "failed to reassign: no matches found on update()\n";
    $return = 3;
}

foreach my $ret (@return) {
    if (ref $ret && $ret->{number} eq $inc->{number}) {
        print scalar $SNOW->tkt_string_base ($SNOW->incident_by_number($incnum));
    } elsif (ref $ret) {
        print "returned object did not match our original incident number";
        print scalar $SNOW->tkt_string_base ($ret);
        $return = 1;
    } else {
        print "error: $ret\n";
        $return = 1;
    }
}

exit $return;

##############################################################################
### Final Documentation ######################################################
##############################################################################

=head1 OPTIONS

=over 4

=item I<INCIDENT_NUMBER>

ID of an incident number to update.  You can leave off the F<INC0+> values.

=item I<TEXT>

Text to enter into the journal entry.

=item B<--config> I<CONFIG>

=item B<--debug>

Print debugging information on STDERR.

=item B<--user> I<USER>

The user listed in the submitted text; defaults to $ENV{'USER'} or the running uid.

=item B<--help>

Prints out basic full help documentation and exits.

=item B<--man>

Prints out the full help documentation and exits.

=back

=head1 REQUIREMENTS

B<FNAL::SNOW>

=head1 EXAMPLES

    snow-incident-journal 389936 "testing"

=head1 AUTHOR

Tim Skirvin <tskirvin@fnal.gov>

=head1 LICENSE

Copyright 2014, Fermi National Accelerator Laboratory

This program is free software; you may redistribute it and/or modify it under
the same terms as Perl itself.

=cut
