#!/usr/bin/env perl

=head1 NAME

snow-incident-list - lists matching Service Now incidents

=head1 SYNOPSIS

B<snow-incident-list> --type assign --user I<USER>

B<snow-incident-list> --type group 

B<snow-incident-list> --type submit --user `whoami` 

B<snow-incident-list> --type unassigned --group I<GROUP>

B<snow-incident-list> --type unresolved --date "now"

B<snow-incident-list> --man

=head1 DESCRIPTION

snow-incident-list prints ticket summaries for matching entries in the Service
Now incident table.  

Note that each query can match a maximum of 250 entries.

=cut

##############################################################################
### Configuration ############################################################
##############################################################################

use lib '/home/tskirvin/rpm/fnal-snow/lib';
our $CONFIG_FILE = '/home/tskirvin/rpm/fnal-snow/etc/snow/config.yaml';

## Default values, these should all be accessible via command-line parameters
our $DATE    = 'now';
our $DEBUG   = 0;
our $GROUP   = 'all';
our $SUBTYPE = 'open';
our $TYPE    = 'group';
our $USER    = getpwuid ($>);

##############################################################################
### Declarations #############################################################
##############################################################################

use strict;
use warnings;

use Date::Manip;
use Getopt::Long;
use Pod::Usage;
use FNAL::SNOW;

$|++;

##############################################################################
### Subroutines ##############################################################
##############################################################################

# Exit out with pod2usage.
sub error_usage {
    my ($error) = @_;
    pod2usage (-exit_status => 2, -verbose => 1);
}

### debug (MSG)
# Print a debugging message if $DEBUG is set.
sub debug { if ($DEBUG) { warn "@_\n" } }

##############################################################################
### main () ##################################################################
##############################################################################

my $parser = Getopt::Long::Parser->new();
my $result = $parser->getoptions (
    't|type=s'    => \$TYPE,
    'g|group=s'   => \$GROUP,
    'u|user=s'    => \$USER,
    'debug'       => \$DEBUG,
    'date=s'      => \$DATE,
    'c|config'    => \$CONFIG_FILE,
    's|subtype=s' => \$SUBTYPE,
    'man'      => sub { pod2usage (-verbose => 2, -noperldoc => 1) },
    'h|help'   => sub { pod2usage (-verbose => 1) }) || error_usage ();

debug "Creating FNAL::SNOW object: $CONFIG_FILE";
my $SNOW = FNAL::SNOW->init ('config_file' => $CONFIG_FILE, 'debug' => $DEBUG);
my $CONFIG = $SNOW->config_hash;

debug "Connecting to ServiceNow at $CONFIG->{servicenow}->{url}";
$SNOW->connect or die "could not connect to SN\n";

## If we got '--group all', then we want to parse all groups that a given user
### belongs to; otherwise, we just want to use that user as the grouo name

our @GROUPS;
if (lc $GROUP eq 'all') { @GROUPS = $SNOW->user_in_groups ($USER) }
else                    { @GROUPS = $GROUP }

my $time = UnixDate ($DATE, "%s") || time;

## TODO: move all of these to functions

my @return;
if    (lc $TYPE eq 'unresolved') {
    foreach my $grp (@GROUPS) {
        push @return, $SNOW->text_inclist_unresolved ($grp, $time), '';
    }
}

elsif (lc $TYPE eq 'unassigned') {
    foreach my $grp (@GROUPS) {
        push @return, $SNOW->text_inclist_unassigned ($grp);
    }
}

elsif (lc $TYPE eq 'group') {
    foreach my $grp (@GROUPS) {
        push @return, $SNOW->text_inclist_group ($grp, $SUBTYPE), '';
    }
}

elsif (lc $TYPE eq 'assign') {
    push @return, $SNOW->text_inclist_assignee ($USER, $SUBTYPE);
}

elsif (lc $TYPE eq 'submit') {
    push @return, $SNOW->text_inclist_submit ($USER, $SUBTYPE);
}

else { pod2usage( -verbose => 1 ) }

print join ("\n", @return, '');

exit 0;

##############################################################################
### Final Documentation ######################################################
##############################################################################

=head1 OPTIONS

=over 4

=item B<--config> F<FILE>

Which configuration file should we load?  The default points at
F</etc/snow/config.yaml>.

=item B<--date> F<DATESTRING>

Any string that can be parsed by B<Date::Manip> into a seconds-since-epoch
string.  Used in B<--type unresolved> to determine a start date for some ticket
queries.  

Defaults to 'now'.  Applies to type F<unresolved>.

=item B<--group> F<GROUP>

Run the query with the group F<GROUP>.  If F<GROUP> is I<all>, then we will
select groups based on the value of F<USER> (which defaults to the running
user).

=item B<--type> [unresolved|unassigned|group|assign|submit]

What kind of query should we run?

=over 4

=item assign

Show tickets assigned to user F<USER>.

=item group

Show tickets assigned to group F<GROUP>.  

Default: 'all'.

=item submit

Show tickets submitted by user F<USER>.

=item unassigned

Show tickets in group(s) F<GROUP> that have not been assigned to a person.

=item unresolved

Show tickets in group(s) F<GROUP> that have not been resolved, and which were 
submitted before the date specified in I<DATE>.

=back

Default: 'group'

=item B<--subtype> [open|closed|unresolved]

For most query types we can further filter based on subtypes.  Specifically:

    closed          Only show closed tickets.
    open            Only show open tickets.     (default)
    unresolved      Only show unresolved tickets.

Applies to types F<group>, F<assign>, or F<submit>.

=item B<--user> F<USER>

Run the query with user F<USER>.  Defaults to the user running the program.

=item B<--debug>

Print debugging information on STDERR.

=item B<--help>

Prints out basic full help documentation and exits.

=item B<--man>

Prints out the full help documentation and exits.

=back

=head1 REQUIREMENTS

B<FNAL::SNOW>

=head1 EXAMPLES

    snow-incident-list --type assign
    snow-incident-list --type group --user tskirvin
    snow-incident-list --type submit --user tskirvin --subtype closed
    snow-incident-list --type unassigned --group CMS-CIS
    snow-incident-list --type unresolved --group all --user tskirvin

=head1 AUTHOR

Tim Skirvin <tskirvin@fnal.gov>

=head1 LICENSE

Copyright 2014, Fermi National Accelerator Laboratory

This program is free software; you may redistribute it and/or modify it under
the same terms as Perl itself.

=cut
