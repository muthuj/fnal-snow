#!/usr/bin/env perl

=head1 NAME

snow-ticket-journal - adds a journal entry for a Service Now ticket

=head1 SYNOPSIS

snow-ticket-journal INC389936 "testing"

snow-ticket-journal INC389936 --type comment "testing2"

snow-ticket-journal INC389936 --type work_notes "testing3"

echo "testing4" | snow-ticket-journal INC389936 --type work_notes --stdin

B<snow-ticket-journal> --man

=head1 DESCRIPTION

snow-ticket-journal adds new journal entries for a specified ticket number.
The updated ticket information is then printed to STDOUT.

=cut

##############################################################################
### Configuration ############################################################
##############################################################################

our $CONFIG_FILE = '/etc/snow/config.yaml';

our $DEBUG = 0;
our $TYPE  = 'work_notes';
our $USER  = $ENV{'REMOTE_USER'} || $ENV{'USER'} || getpwuid ($>);
our $STDIN = 0;

##############################################################################
### Declarations #############################################################
##############################################################################

use strict;
use warnings;

use Getopt::Long;
use Pod::Usage;
use FNAL::SNOW;

$|++;

##############################################################################
### Subroutines ##############################################################
##############################################################################

### error_usage (ERROR)
# Exit out with pod2usage.
sub error_usage {
    my ($error) = @_;
    pod2usage (-msg => $error, -exit_status => 2, -verbose => 1);
}

### debug (MSG)
# Print a debugging message if $DEBUG is set.
sub debug { if ($DEBUG) { warn "@_\n" } }

##############################################################################
### main () ##################################################################
##############################################################################

my $parser = Getopt::Long::Parser->new();
my $result = $parser->getoptions (
    'd|debug'    => \$DEBUG,
    'c|config=s' => \$CONFIG_FILE,
    'u|user=s'   => \$USER,
    't|type=s'   => \$TYPE,
    'stdin'      => \$STDIN,
    'man'        => sub { pod2usage (-verbose => 2, -noperldoc => 1) },
    'h|help'     => sub { pod2usage (-verbose => 1) }) || error_usage ();

error_usage ("invalid type: $TYPE") unless $TYPE =~ /^(work_notes|comment)$/;

my ($tktnum, @rest) = @ARGV;
error_usage ("no tkt listed") unless ($tktnum);

my $text = join (' ', @rest);
if ($STDIN) { $text = join ("\n", $text, <STDIN>) }

error_usage ('no text provided') if $text eq '';

debug "Creating FNAL::SNOW object: $CONFIG_FILE";
my $SNOW = FNAL::SNOW->init ('config_file' => $CONFIG_FILE, 'debug' => $DEBUG);
my $CONFIG = $SNOW->config_hash;

debug "Connecting to ServiceNow at $CONFIG->{servicenow}->{url}";
$SNOW->connect or die "could not connect to SN\n";

debug "Looking up user '$USER'";
my $u = $SNOW->user_by_username ($USER);
unless ($u) { die "invalid user: $USER\n" }
my $uid = $u->{'sys_id'};

debug "Looking up tkt '$tktnum'";
my @tkt = $SNOW->tkt_by_number ($tktnum);
unless (@tkt) { die "unable to load ticket: '$tktnum'\n"; }
unless (scalar @tkt == 1) {
    die "too many matches for ticket '$tktnum'\n";
}
my $tkt = $tkt[0];

debug "Adding journal entry";

my @return = $SNOW->tkt_update ($tkt,
    $TYPE => "$USER: $text", 'type' => $TYPE, 'user' => $USER);

my $return = 0;
if (scalar @return > 1) {
    print "too many matches after update\n";
    $return = 2;
} elsif (scalar @return < 1) {
    print "failed to reassign: no matches found on update()\n";
    $return = 2;
}

foreach my $ret (@return) {
    my $error = $SNOW->tkt_compare ($tkt, $ret);
    if ($error) {
        print "error: $error\n";
        $return = 1;
    } else {
        print scalar $SNOW->tkt_string_base ($ret)
    }
}

exit $return;

##############################################################################
### Final Documentation ######################################################
##############################################################################

=head1 OPTIONS

=over 4

=item I<TICKET_NUMBER>

ID of a ticket number to update.

=item I<TEXT>

Text to enter into the journal entry.

=item B<--config> I<CONFIG>

=item B<--debug>

Print debugging information on STDERR.

=item B<--user> I<USER>

The user listed in the submitted text; defaults to $ENV{'USER'} or the running uid.

=item B<--stdin>

If set, take additional text for the journal entry on STDIN.

=item B<--help>

Prints out basic full help documentation and exits.

=item B<--man>

Prints out the full help documentation and exits.

=back

=head1 REQUIREMENTS

B<FNAL::SNOW>


=head1 AUTHOR

Tim Skirvin <tskirvin@fnal.gov>

=head1 LICENSE

Copyright 2014-2015, Fermi National Accelerator Laboratory

This program is free software; you may redistribute it and/or modify it under
the same terms as Perl itself.

=cut
